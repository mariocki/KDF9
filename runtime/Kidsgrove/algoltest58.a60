
'begin' 'comment' 'library' A0;
   'comment' program to integrate f(x,y) over triangle (0,0), (1,0), (1,1);
   'comment' using Jensen's device -- David Ho;

   'real' x, y;

   'real' 'procedure' f(x, y);
   'value' x,y;  'real' x,y;
      f:= x + x + y + y;

   'real' 'procedure' int(a, b, x, f);
   'comment' integrates f(x) over range a to b;
   'value' a, b; 'real' a, b, x, f;
   'begin'
      'real' 'procedure' intint(a, b, fa, fb);
      'value' a, b, fa, fb;  'real' a, b, fa, fb;
      'begin'
         'real' c, fc, ff;
         x := c := (a + b) / 2;
         fc := f;
         ff := (fa + 4*fc + fb)/6;
         'comment' answer is good enough if simrule no better than trapezoidal;
         'if'  abs( (fa + fb)/2 - ff ) < 0.001
         'then'
            intint := ff * (b - a)
         'else'
            intint := intint(a, c, fa, fc) + intint(c, b, fc, fb);
      'end';
      'real' fa;
      x := a;   fa := f;  x := b;
      int := intint(a, b, fa, f);
   'end';

   'real' answer;

   'real' 'procedure' minitest(x);
   'value' x;  'real' x;
   'begin'
      minitest := x*x*x;
   'end';

   writetext(30, {minitest_0_to_2_x^3{c}});
   answer := int(0, 2, x, minitest(x));
   output(30, answer);
   output(30, answer*100);

'comment'   writetext(30, {fulltest_0_to_1_triangle_=_});
'comment'   answer := int(0, 1, x, int(0, x, y, f(x,y)));
'comment'   output(30, answer*100);

'end'
