V20; W0;
RESTART; MRWDQ0; ZERO; MRWDQ0;
PROGRAM;                   (erase tape label);

(test WIPE);

   V0 = PEFPBEAAG;         (tape label);
   V1 = Q0/AV2/AV3;        (to read label data);
   V2 = 0;                 (for TSN);
   V3 = 0;                 (for label);

   V4 = Q0/AV5/AV5;        (for TP0 tracing output);
   V5 = 0;                 (tracing word);

   V13 = B6445636400470277;  (TEST G);
   V14 = B5441424554004202;  (LABEL B);
   V15 = B5441424554004602;  (LABEL F);
   V16 = B6762576445005402;  (WROTE L);
   V17 = B6245675765564402;  (REWOUND);
   V18 = B6751604500575302;  (WIPE OK);
   V19 = B4651565163500277;  (FINISH);

   (claim tape);
   V0; SET4; OUT;
   V1; =Q1; =C1;

   (claim TP0);
   SET 1; SET 5; OUT; V4; =Q10; =C10;
   V13; =V5; POAQ10;

   MFRQ1; (read label);
   V0; V3; DUPD; NEV; J777NEZ;
   ERASE; ERASE;
   V15; =V5; POAQ10;

   SET-1; DUP; DUPD;
   =V2; =V3;
   MBRQ1; (backward read same label);
   (now compare the two results);
   V0; V2; DUPD; NEV; J777NEZ;
   ERASE; ERASE;
   (the two reads match);
   V14; =V5; POAQ10;

   MFRQ1; (read label);
   V0; V3; DUPD; NEV; J777NEZ;
   ERASE; ERASE;
   V15; =V5; POAQ10;

   MLWQ1; (write 2nd copy of label as last block);
   V16; =V5; POAQ10;

   MRWDQ1; (rewind);
   V17; =V5; POAQ10;

   Q1 TO Q4; SET125; =M4;
   MWIPEQ4; (wipe entire unwiped area);
   V18; =V5; POAQ10;

   (FINISH message);
   V19; =V5; POAQ10;

   ZERO; OUT;

777;
   MRWDQ0; (deliberately fail);

FINISH;
|
