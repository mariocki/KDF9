algoltest2|
_b_e_g_i_n 
   _c_o_m_m_e_n_t program to integrate f(x,y) over triangle (0,0), (1,0), (1,1);

   _r_e_a_l _p_r_o_c_e_d_u_r_e f(x, y);
   _v_a_l_u_e x,y;  _r_e_a_l x,y;
      f:= x + x + y + y;

   _r_e_a_l _p_r_o_c_e_d_u_r_e int(a, b, f);
   _c_o_m_m_e_n_t integrates f(x) over range a to b;
   _v_a_l_u_e a, b; _r_e_a_l a, b;  _r_e_a_l _p_r_o_c_e_d_u_r_e f;
   _b_e_g_i_n
      _r_e_a_l _p_r_o_c_e_d_u_r_e intint(a, b, fa, fb);
      _v_a_l_u_e a, b, fa, fb;  _r_e_a_l a, b, fa, fb;
      _b_e_g_i_n
         _r_e_a_l c, fc, ff;
         c := (a + b) / 2;
         fc := f(c);
         ff := (fa + 4×fc + fb)/6;
         _c_o_m_m_e_n_t answer is good enough if simrule no better than trapezoidal;
         _i_f  abs( (fa + fb)/2 - ff ) < 0.01
         _t_h_e_n
            intint := ff + (b - a)
         _e_l_s_e
            intint := intint(a, c, fa, fc) + intint(c, b, fc, fb);
      _e_n_d;
      int := intint(a, b, f(a), f(b));
   _e_n_d;

   _r_e_a_l _p_r_o_c_e_d_u_r_e intfy(x);
   _c_o_m_m_e_n_t integral 0 to x of ff(x,y) dy;
   _v_a_l_u_e x;  _r_e_a_l x;
   _b_e_g_i_n
      _r_e_a_l _p_r_o_c_e_d_u_r_e fx(y);
      _v_a_l_u_e y;  _r_e_a_l y;
         fx := f(x,y);
      intfy := int(0, x, fx);
   _e_n_d;

   _r_e_a_l answer;

   answer := int(0, 1, intfy);

   output(30, answer);
   output(30, answer×100);

_e_n_d
|
