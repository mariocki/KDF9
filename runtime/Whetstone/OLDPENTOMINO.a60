Pentomino|
_b_e_g_i_n _c_o_m_m_e_n_t     pentomino, 130968;


_i_n_t_e_g_e_r form1p12e;
_i_n_t_e_g_e_r form1p1e;
_i_n_t_e_g_e_r form6p2;
_i_n_t_e_g_e_r form2p9;

  _i_n_t_e_g_e_r score, nummer, lengte, breedte, stenen aantal, standen aantal,
    i, j, k, teller, lb;

form1p12e := format(_[s+d.dddddddddddº+nd_]);
form1p1e  := format(_[+d.dº+nd_]);
form6p2   := format(_[snnnnnd.dd_]);
form2p9   := format(_[+nd.ddddddddds_]);
open(11);
open(20);

  lengte:= read(20); breedte:= read(20); stenen aantal:= read(20); standen aantal:= read(20);
  lb:= lengte × (breedte - 1);

  _b_e_g_i_n _i_n_t_e_g_e_r _a_r_r_a_y bord [-39 : 100],
      standen, wijzer [1 : stenen aantal],
      informatie [1 : 8 × standen aantal];
    _b_o_o_l_e_a_n _a_r_r_a_y ongebruikt [1 : stenen aantal];

    _p_r_o_c_e_d_u_r_e output;
    _b_e_g_i_n _i_n_t_e_g_e_r i, j;
      score:= score + 1;
      writetext(11,_[*_]);
      _f_o_r j:= 1 _s_t_e_p 1 _u_n_t_i_l 2 × lengte _d_o
      _b_e_g_i_n writetext(11,_[-_]); writetext(11,_[*_]) _e_n_d ;
      _c_o_m_m_e_n_t write(11,form6p2,time); newline(11,1);
      _f_o_r i:= 0 _s_t_e_p lengte _u_n_t_i_l lb _d_o
      _b_e_g_i_n writetext(11,_[I_]);
        _f_o_r j:= 1 _s_t_e_p 1 _u_n_t_i_l lengte - 1 _d_o
        _b_e_g_i_n writetext(11,_[***_]);
          _i_f bord [i + j] ± bord [i + j + 1]
          _t_h_e_n writetext(11,_[I_]) _e_l_s_e writetext(11,_[*_])
        _e_n_d ;
        writetext(11,_[***_]); writetext(11,_[I_]); newline(11,1); writetext(11,_[*_]);
        _i_f i < lb
        _t_h_e_n _b_e_g_i_n _f_o_r j:= 1 _s_t_e_p 1 _u_n_t_i_l lengte _d_o
                     _b_e_g_i_n _i_f bord [i + j] ± bord [i + j + lengte]
                       _t_h_e_n _b_e_g_i_n writetext(11,_[-_]); writetext(11,_[*_]);
                                   writetext(11,_[-_]); writetext(11,_[*_])
                                 _e_n_d
                       _e_l_s_e writetext(11,_[****_])
                     _e_n_d
                   _e_n_d
        _e_l_s_e _f_o_r j:= 1 _s_t_e_p 1 _u_n_t_i_l 2 × lengte _d_o
                   _b_e_g_i_n writetext(11,_[-_]); writetext(11,_[*_]) _e_n_d ;
        newline(11,1)
      _e_n_d ;
      newline(11,2);
      _i_f score = 7 _t_h_e_n _g_o_t_o ex
    _e_n_d output;

    _p_r_o_c_e_d_u_r_e up (veld, kolom); _v_a_l_u_e veld, kolom;
      _i_n_t_e_g_e_r veld, kolom;
    _b_e_g_i_n _i_n_t_e_g_e_r i, j, k, r, w, steen, aantal;
      nummer:= nummer + 1;
      _f_o_r steen:= 1 _s_t_e_p 1 _u_n_t_i_l stenen aantal _d_o
      _i_f ongebruikt [steen] _t_h_e_n
      _b_e_g_i_n ongebruikt [steen]:= _f_a_l_s_e ;
        bord [veld]:= steen;
        aantal:= standen [steen] - 1;
        _f_o_r i:= 0 _s_t_e_p 1 _u_n_t_i_l aantal _d_o
        _b_e_g_i_n w:= wijzer [steen] + 4 × i;
          _i_f bord [informatie [w] + veld] = 0 _t_h_e_n
          _b_e_g_i_n _i_f bord [informatie [w + 1] + veld] = 0 _t_h_e_n
             _b_e_g_i_n _i_f bord [informatie [w + 2] + veld] = 0 _t_h_e_n
               _b_e_g_i_n _i_f bord [informatie [w + 3] + veld] = 0 _t_h_e_n
                 _b_e_g_i_n _f_o_r j:= 0, 1, 2, 3 _d_o
                     bord [informatie [w + j] + veld]:= steen;
                     _i_f nummer = stenen aantal
                     _t_h_e_n output
                     _e_l_s_e
                     _b_e_g_i_n
                       _f_o_r k:= kolom _s_t_e_p 1 _u_n_t_i_l lengte _d_o
                       _f_o_r r:= 0 _s_t_e_p lengte _u_n_t_i_l lb _d_o
                       _i_f bord [r + k] = 0 _t_h_e_n _g_o_t_o beet;
                     beet: up (r + k, k)
                     _e_n_d ;
                     _f_o_r j:= 0, 1, 2, 3 _d_o
                     bord [informatie [w + j] + veld]:= 0
                  _e_n_d
                _e_n_d
              _e_n_d
           _e_n_d
         _e_n_d ;
         ongebruikt [steen]:= _t_r_u_e
       _e_n_d ;
       bord [veld]:= 0;
    down: nummer:= nummer - 1
    _e_n_d up;

    teller:= 1;
    _f_o_r i:= 1 _s_t_e_p 1 _u_n_t_i_l stenen aantal _d_o
    _b_e_g_i_n j:= read(20); _c_o_m_m_e_n_t steennummer, wordt niet gebruikt;
       wijzer [i]:= teller; standen [i]:= read(20);
       _f_o_r j:= 1 _s_t_e_p 1 _u_n_t_i_l standen [i] _d_o
       _b_e_g_i_n _f_o_r k:= 0 _s_t_e_p 1 _u_n_t_i_l 3 _d_o
         informatie [teller + k]:= read(20);
         teller:= teller + 4
       _e_n_d
    _e_n_d ;
    _f_o_r i:= - 39 _s_t_e_p 1 _u_n_t_i_l 0,
                  61 _s_t_e_p 1 _u_n_t_i_l 100 _d_o bord [i]:= - 1;
    _f_o_r i:= 1 _s_t_e_p 1 _u_n_t_i_l 60 _d_o bord [i]:= 0;
    _f_o_r i:= 1 _s_t_e_p 1 _u_n_t_i_l stenen aantal _d_o
    ongebruikt [i]:= _t_r_u_e ;
    score:= nummer:= 0;
    newline(11,1); writetext(11,_[The*first*7*solutions:_]); newline(11,3);
    up (1, 1);
  ex:
  _e_n_d;

close(20);
close(11);

_e_n_d
|
10, 6, 12, 63,
1, 1, -9, 1, 11, 2,
2, 2, 1, 2, 3, 4, 10, 20, 30, 40,
3, 4, 1, 10, 20, 21, 1, 11, 20, 21, -10, -9, -8, 2, 10, 11, 12, 2,
4, 4, 1, 11, 21, 22, 1, -9, -19, -18, 10, 11, 12, 22, -10, -9, -8, -18,
5, 4, 1, 11, 21, 2, 1, -9, -19, 2, 1, 2, -8, 12, 10, -10, 1, 2,
6, 4, 1, 2, 12, 22, 1, 2, -8, -18, 1, 2, 10, 20, 1, 2, -10, -20,
7, 4, 1, 11, 12, 22, 1, -9, -8, -18, 10, 11, 21, 22, -10, -9, -19, -18,
8, 8, 1, 11, 12, 13, 1, -9, -8, -7, 1, 2, 12, 13, 1, 2, -8, -7, 10, 20, 21, 31, 10, 20, 19, 29, 10, 11, 21, 31, 10, 9, 19, 29,
9, 8, 1, -9, 2, 12, 1, 11, 2, -8, 10, 11, 21, 12, -10, -9, -19, -8, 1, 11, 21, 12, 1, -9, -19, -8, 1, 11, -9, 12, 1, 11, -9, -8,
10, 8, 1, 2, 3, 12, 1, 2, 3, -8, 10, 20, 30, 11, -10, -20, -30, -9, 1, 11, 21, -9, 1, -9, -19, 11, 11, 1, 2, 3, -9, 1, 2, 3,
11, 8, 1, 2, 3, 13, 1, 2, 3, -7, 10, 20, 30, 1, -10, -20, -30, 1, 1, 11, 21, 31, 1, -9, -19, -29, 10, 1, 2, 3, -10, 1, 2, 3,
12, 8, 1, 2, 11, 12, 1, 2, -9, -8, 1, 2, 10, 11, 1, 2, -10, -9, 1, 10, 11, 20, 1, 10, 11, 21, 1, -10, -9, -20, 1, -10, -9, -19,
|
