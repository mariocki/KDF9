GPSPRIMES|
 _b_e_g_i_n

 _c_o_m_m_e_n_t after Knuth and Merner, CACM June 1961;

 _i_n_t_e_g_e_r  _p_r_o_c_e_d_u_r_e GPS(j, n, z, v);  _i_n_t_e_g_e_r j, n, z, v;
    _b_e_g_i_n
    _f_o_r j := 1  _s_t_e_p 1  _u_n_t_i_l n  _d_o z := v;
    GPS := 1
    _e_n_d;

 _i_n_t_e_g_e_r  _p_r_o_c_e_d_u_r_e rem(n, d);  _v_a_l_u_e n, d;  _i_n_t_e_g_e_r n, d;
    rem := n - d × (n ÷ d);

 _b_o_o_l_e_a_n  _p_r_o_c_e_d_u_r_e is special(n, r);  _v_a_l_u_e n, r;  _i_n_t_e_g_e_r n, r;
    is special := rem(n, 10) = r  _a_n_d n ÷ 10  ± 1;

 _i_n_t_e_g_e_r j, a, m, p, z;

open(10);

 _f_o_r m := 1  _s_t_e_p 1  _u_n_t_i_l 30 ×
   GPS
      (
       j,
        _i_f j = 0  _t_h_e_n -1  _e_l_s_e j,
       p,
        _i_f j = 1  _t_h_e_n  1
                    _e_l_s_e
                       _i_f GPS
                              (
                               a,
                               j,
                               z,
                                _i_f a = 1  _t_h_e_n 1
                                           _e_l_s_e ( _i_f rem(j, a) = 0  _a_n_d a < j _t_h_e_n 0 _e_l_s_e z)
                              ) = z
                       _t_h_e_n ( _i_f p < m  _t_h_e_n p + 1 _e_l_s_e j × GPS(a, 1, j, -1))
                       _e_l_s_e p
      )
 _d_o
    _b_e_g_i_n
    writetext(10, _[The _[s_]_]);
    write(10, format(_[nd_]), m);
    _i_f is special(m) in having units digit:(1)  _t_h_e_n writetext(10, _[st * _])  _e_l_s_e
    _i_f is special(m) in having units digit:(2)  _t_h_e_n writetext(10, _[nd * _])  _e_l_s_e
    _i_f is special(m) in having units digit:(3)  _t_h_e_n writetext(10, _[rd * _])  _e_l_s_e
                                                           writetext(10, _[th * _]);
    writetext(10, _[prime * is * _]);
    write(10, format(_[nddc_]), p);
    _e_n_d;

close(10)

_e_n_d
|
|
