V256; W0; YA4096;
RESTART; J777; J777;
PROGRAM;                   (main program);

(tests MBRQq immediately after the label, and R/W minimal blocks);
(should terminate normally);

   V0 = PEFPBEAAG;            (tape label);
   V1 = Q0/AV2/AV2;           (for TP0 trace o/p);
   V2 = 0;                    (TP0 trace o/p word);
   V3 = Q0/AV4/AV4;           (for small block write);
   V4 = B4142754344754546;    (AB|CD|EF);
   V5 = B4142750000000000;    (AB|     ); (after MFRE);
   V6 = B0000000000754546;    (     |EF); (after MW; MBRE);
   V7 = B0000750000000000;    (  |     ); (after MWE; MBRE);

   V8 = Q0/AV50 /AV059;       (to write the test data block);
   V9 = Q0/AYA0/AYA4095;      (to read it back backwards);

   (test data block source);
   V50 = B6445636400530277;   (TEST K);
   V51 = B4663536000575302;   (FSKP OK);
   V52 = B6762576445027777;   (WROTE);
   V53 = B5542620057530277;   (MBR OK);
   V54 = B2167576244027777;   (1WORD);
   V55 = B5546624500575302;   (MFRE OK);
   V56 = B5542624500575302;   (MBRE OK);
   V57 = B5567450021027777;   (MWE 1);
   V59 = B4651565163500277;   (FINISH);

   (claim the MT);
   V0; SET4; OUT; =RC1;

   (claim TP0);
   SET 1; SET 5; OUT; V1; =Q10; =C10;
   V50; =V2; POAQ10;

   (skip over the label);
   Q1 TO Q7; SET 1; =M1;
   MFSKQ1;
   V51; =V2; POAQ10;

   (check reading and writing);

   (write the test data block, of length 10 words);
   V8; =Q8; C1; =C8;
   MWQ8;
   V52; =V2; POAQ10;

   (read the test data backwards, attempting to read 4096 words; only 10 exist);
   V9; =Q9; C1; =C9;
   MBRQ9;
   YA4095; DUP; J777NEZ; ERASE;
   V50; YA9; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V56; YA3; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V59; YA0; DUPD; NEV; J777NEZ; ERASE; ERASE;
   MFRQ9;
   V53; =V2; POAQ10;

   (check reading and writing minimal blocks);

   (write 1 word containing an end message);
   V3; = Q3; C1 TO Q3;
   MWQ3;
   V54; =V2; POAQ10;

   SETAV100; DUP; =I1; =M1;
   MBRQ1;
   V100; V4; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V53; =V2; POAQ10;

   MFREQ1;
   V100; V5; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V55; =V2; POAQ10;

   MBREQ1;
   V100; V6; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V56; =V2; POAQ10;
   MFREQ1;

   (ditto, writing second small block to end message);
   MWEQ3;
   V57; =V2; POAQ10;

   MBRQ1;
   V100; V5; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V53; =V2; POAQ10;

   MFREQ1;
   V100; V5; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V55; =V2; POAQ10;

   MBREQ1;
   V100; V7; DUPD; NEV; J777NEZ; ERASE; ERASE;
   V56; =V2; POAQ10;

   (FINISH message);
   V59; =V2; POAQ10;

   (release MT);
   C1; SET 7; OUT;

   (release TP0);
   C10; SET 6; OUT;

   ZERO; OUT;

777;
   MRWDQ0; (deliberately fail, with failing values in N1,N2);

FINISH;
|
