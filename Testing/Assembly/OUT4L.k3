V256; W0; YA4096;
RESTART; J777; J777;
PROGRAM;                   (main program);

(tests MBRQq immediately after the label, and R/W maximal blocks);
(should terminate normally);

   V0 = PEFPBEAAG;            (tape label);
   V1 = Q0/AV2/AV2;           (for TP0 trace o/p);
   V2 = 0;                    (TP0 trace o/p word);
   V3 = Q0/AV4/AV4;           (for small block write);
   V4 = B4142754344754546;    (AB|CD|EF);
   V5 = B4142750000000000;    (AB|     ); (after MFRE);
   V6 = B0000000000754546;    (     |EF); (after MW; MBRE);
   V7 = B0000750000000000;    (  |     ); (after MWE; MBRE);

   V8 = Q0/0/32767;           (to write the test data block);

   (test data block source);
   V50 = B6445636400540277;   (TEST L);
   V51 = B4263536000575302;   (BSKP OK);
   V52 = B6762576445027777;   (WROTE);
   V53 = B4663536000575302;   (FSKP OK);
   V55 = B5546620000575302;   (MFR OK);
   V59 = B4651565163500277;   (FINISH);

   (claim the MT);
   V0; SET4; OUT; =RC1;

   (claim TP0);
   SET 1; SET 5; OUT; V1; =Q10; =C10;
   V50; =V2; POAQ10;

   (skip over the label);
   Q1 TO Q7; SET 1; =M1;
   MFSKQ1;
   V53; =V2; POAQ10;

   (check reading and writing);

   (check reading and writing maximal blocks);

   (put FINISH in the last word of core);
   V59; =E32767;

   (write 32K words);
   V8; = Q8; C1 TO Q8;
   MWQ8;
   V52; =V2; POAQ10;

   SET 1; =M8;
   MBSKQ8;
   V51; =V2; POAQ10;

   (put 0 in the last word of core);
   ZERO; =E32767;

   V8; = Q8; C1 TO Q8;
   MFRQ8;
   (check that E32768 has been restored);
   E32767; V59; DUPD; NEV; J777NEZ;
   ERASE; ERASE;
   V55; =V2; POAQ10;

   (FINISH message);
   V59; =V2; POAQ10;

   (release MT);
   C1; SET 7; OUT;

   (release TP0);
   C10; SET 6; OUT;

   ZERO; OUT;

777;
   MRWDQ0; (deliberately fail);

FINISH;
|
