P
GA3338W03UPU
1 DIMENSIONAL TRANSIENT HEAT FLOW PROGRAM|
ST3348 ; TL 50 ;
V087;     W32;
YA 32;
YB 97;
YC 31;
PROGRAM;

(GA3338W03UPU);
(1 DIMENSIONAL TRANSIENT HEAT FLOW PROGRAM);
(The program described is a usercode equivalent of an Algol program
 described in English Electric Report No. W/M[4B] p.1098

 This is the first stage in developing a two-dimensiona1 transient heat flow
 program.

 This program calculates transient temperatures for radial flow in a cylinder
 or flow between the faces of an infinite slab.

 The boundary conditions at the inner and outer surfaces functions of time.
 They are:
  (a) surface temperature
  (b) Heat transfer coefficient and ambient temperature given
      (the special case of zero heat transfer coefficient allowed)
  (c) Two sets of conditions of the type (b) are given, the program
      choosing the one giving the greater inwards heat flow.
      (Condensative heat transfer is greater than convective.)

      The cylinder, or slab, may be sub-divided into a number of
      region. Each has its own thermal properties and/or node spacing. );


     V0  = Q20/AV1/AYB67;
     V1  = Q0/AYB3/AYB34;
     V2  = Q0/AYB35/AYB66;

     V4  = B 6041 6246 4151 5402; (V4 = PPARFAIL[C]);
     V5  = F2;
     V6  = F1;
     V7  = Q0/AV0P99/AV20P99;
     V8  = B30;
     (V9 IS ??);
     (V10/V11 IS A FAILURE MESSAGE FOR THE CURRENT CONTEXT);
     V12 = B 5657 6436 4136 6641; (V12 = PNOT*A*VA);
     V13 = B 5451 4436 0200 0000; (V13 = PLID*[DC]);

     V14 = Q0/AV9/AV11;
     V15 = B 6041 6251 6471 3646; (V15 = PPARITY*F);
     V16 = B 4151 5465 6245 3602; (V16 = PAILURE*[DC]);

     V17 = B 4555 3642 4546 5762; (V17 = PEM*BEFOR);
     V18 = B 4500 5665 5542 4562; (V18 = PE NUMBER[DC]);

     V19 = B 6467 5736 4555 3657; (V19 = PTWO*EP*O );
     V20 = B 5636 6441 6045 0200; (V20 = PN*TAPE[DC]);

     V21 = B 4651 7045 4436 6064; (V21 = PFIXED*PT );
     V22 = B 3646 4151 5402 0200; (V22 = P*FAIL[2DC]);

     V23 = B 4654 5741 6447 3600; (V23 = PFLOATG*PT*FAIL[DC]);
     V24 = B 6436 4641 5154 0200; (V24 = PFLOATG*PT*FAIL[DC]);

     V25 = B 0200 0000 0000 0000; (V25 = P[8DP]);  (TOO MUCH SPACING. REMOVED 7 LFS);
     V26 = B 0000 0000 0000 0002; (V26 = P[7DP]);  (TOO MUCH SPACING. REMOVED 6 LFS);

     V27 = Q0/AYC0/AYC15;

     V28 = B 0044 4164 4136 6062; (V28 = P DATA*PR);
     V29 = B 5746 5762 5541 3646; (V29 = POFORMA*F);

     V30 = B 5154 5445 4436 5156; (V30 = PILLED*IN);
     V31 = B 4357 6262 4543 6454; (V31 = PCORRECTL);
     V32 = B 7100 0000 5156 4357; (V32 = PY . INCO);
     V33 = B 6262 4553 6436 4556; (V33 = PRRECT*EN);
     V34 = B 6462 7136 4245 4657; (V34 = PTRY*BEFO);
     V35 = B 6245 3664 5045 4657; (V35 = PRE*THESE);
     V36 = B 3643 5041 6241 4364; (V36 = P*CHARACT);
     V37 = B 4562 6336 3602 0202; (V37 = PERS**[5DC]);

     V39 = B 5156 5645 6236 3642; (V39 = PINNER**B);
     V40 = B 5765 5644 4162 7136; (V40 = POUNDARY*);
     V41 = B 4357 5644 5164 5157; (V41 = PCONDITIO);
     V42 = B 5663 0202 0202 0200; (V42 = PNS[5DC]);

     V43 = B 5765 6445 6236 3642; (V43 = POUTER**B);

     V44 = B 6450 4562 5541 5436; (V44 = PTHERMAL*);
     V45 = B 6062 5760 4562 6471; (V45 = PPROPERTI);
     V46 = B 4563 3602 0202 0200; (V46 = PES*[4DC]);

     V47 = B 5156 5164 5141 5436; (V47 = PINITIAL*);
     V48 = B 6445 5560 4562 4164; (V48 = PTEMPERAT);
     V49 = B 6562 4563 0202 0200; (V49 = PURES[3DC]);


     V50 = B 6062 5156 6402 0202; (V50 = PPRINT[3D]);
     V51 = B 6064 4170 0202 0002; (V51 = PTMAX[4D]);

     V52 = B 5156 5645 6236 6241; (V52 = PINNER*RA);
     V53 = B 4451 6563 0202 0002; (V53 = PDIUS[3DC]);


     V54 = B 6241 4451 5136 5657; (V54 = PRADII*NO);
     V55 = B 6436 5156 3641 6343; (V55 = PT*IN*ASC);
     V56 = B 4556 4451 5647 3657; (V56 = PENDING*O);
     V57 = B 6244 4562 0036 0046; (V57 = PRDER.* F);
     V58 = B 4165 5464 7136 6641; (V58 = PAULTY*VA);
     V59 = B 5465 4563 3641 6245; (V59 = PLUES*ARE);

     V60 = B 5756 6336 6451 5545; (V60 = PONS*TIME);
     V61 = B 6336 5657 6436 5156; (V61 = PS*NOT*IN);
     V62 = B 3641 6343 4556 4451; (V62 = P*ASCENDI);
     V63 = B 5647 3657 6344 4562; (V63 = PNG*ORDER);
     V64 = B 0036 4641 6554 6472; (V64 = P.*FAULTY);
     V65 = B 3666 4154 6545 6336; (V65 = P*VALUES*);
     V66 = B 4162 4536 0202 0202; (V66 = PARE*[4D]);

     V67 = B 5541 7036 6245 4465; (V67 = PMAX*REDU);
     V68 = B 4345 4436 6447 3600; (V68 = PCED*TO* );

     V69 = B 5156 4347 6262 4543; (V69 = PINCORREC);
     V70 = B 3646 4441 6441 0002; (V70 = PT*DATA.[C]);

     V71 = B 5657 4445 3662 4144; (V71 = PNODE*RAD);
     V72 = B 5151 0000 0000 0002; (V72 = PII[5Dc]);    (TOO MUCH SPACING. REMOVED 4 LFS);

     V73 = B 3636 3664 5155 4536; (V73 = P***TIME*);
     V74 = B 3636 6445 5560 4562; (V74 = P**TEMPER);
     V75 = B 4164 6562 4563 0002; (V75 = PATURES[c]);

     V76 = Q0/AYC0/AYC2;

     V77 = B 5663 3643 5756 6645; (V77 = PNS*CONVE);
     V78 = B 4364 5157 5636 4156; (V78 = PCTION*AN);
     V79 = B 4436 4357 5644 4556; (V79 = PD*CONDEN);
     V80 = B 6341 6451 5756 3664; (V80 = PSATION*T);
     V81 = B 5155 4563 3636 5657; (V81 = PIMES**NO);
     V82 = B 6436 3645 6165 4154; (V82 = PT**EQUAL);

     V83 = B 5663 3664 0202 0202; (V83 = PNS*T[4D]);

     V84 = B 0044 5163 6441 5643; (V84 = P DISTANC);
     V85 = B 4563 3646 6257 5536; (V85 = PES*FROM*);
     V86 = B 3644 4164 6555 0202; (V86 = P*DATUM[3DC]);

(********************************************************************);
(    GET NUMBER OF CASES                                             );
(********************************************************************);
     V0;     JS1P54;  (JS1L54);   (Initialise paper tape reader);
     SET1;   =RI1;    SETAY0; =RM3;
     JSP104; DUP;     =M3M1Q;  =C3;   (NSD);

(********************************************************************);
(    INPUT DATA CASE                                                 );
(********************************************************************);

     (INITIALIZE STORAGE FOR THE CASE);
1;   SET1;   =RM1;    SETAY0;  =M3;
     SET33;  =RC7;    SETAYA0; =M7;    DC1;
*799;ZERO;   =M0M7Q;  *J799C7NZS; (Clear YA stores);


     SET31;  =RC7;    SET15;   =RC8;
     SETAYC1;DUP;     =M7;     =M8;
*200;ZERO;   =M0M7Q;  *J200C7NZS; (Clear YC stores);

     SET8;   =RC7;    ZERO;    SETB02;

     (INPUT THE TITLE);
198; SET20;  JS1P55;   (JS1L55);  J49;  (Fetch character);
     SHL+42; SHL-42;   J198!=;

     ERASE;
203; SET20;  JS1P55;   (JS1L55);  J49;  (Fetch character);
     SETB43; NOT;      J196!=;    NOT;
     J201;

196; SETB41; NOT;     J202=;   SETB34; J201=;  OR;
206; DC7;    J204C7Z; SHL+6;   J203;

204; =M0M8Q; SET8;    =RC7;    ZERO;   J203C8NZ;
     J207;

202; ERASE;  J206;

201; DC7;    OR;      C7;      SET6;   xD;     CONT;   =C7;
     SHLC7;  =M0M8Q;

     (OUTPUT: TITLE FROM YC STORES);
207; SETB02; =YC15;
     V27;    SETB70;  =YC0;    SET8;   OUT;

     (INPUT: NUMBER OF REGIONS);
     JSP104; DUP;     DUP;     =M3M1Q; =RC4;   (NRt);  =YA19;

     ZERO;   =M3M1Q;
     ZERO;   =M3M1Q;
     ZERO;   =M3M1Q;
     M0M0N;  SHL+24;  SHL-24;  =RM7;   I7=-1;
     M+I7;   SET-1;   =RI8;    M7;     =YA22;
     (INPUT: NUMBER OF INNER AND OUTER BOUNDARY CONDITIONS SPECIFIED);
     JSP104; DUP;     =M7M8Q;  =YA17;  (MA);
     JSP104; DUP;     =M7M8Q;  =YA18;  (MB);

     (CHECK WHETHER A POLAR  OR CARTESIAN CASE IS BEING RUN);
     (If "INNER RADIUS" it is polar);
     SETB44;
208; SET20;  JS1P55;   (JS1L55);  J49;     J208!=; ERASE;
     SETB41;
     SET20;  JS1P55;   (JS1L55);  J49;     REV;    J209!=;
     ERASE;
     SET20;  JS1P56;   (JS1L56);  J51;     =YA21;  J210;

209; SETB51; (Value "I" from "INNER");     J301!=; (Towards end of main program);
     ERASE;

     (SET POLAR MARKER);
     SET1;   =YA14;
     SET20;  JS1P56;   (JS1L56);  J51;     =YA21;
     YA21;   J350=Z;
210; SETB41;
     YA21;   =V0P37;
211; SET20; JS1P55;   (JS1L55); J49;   J211!=; ERASE;
     SETB62;
     SET20; JS1P55;   (JS1L55); J49;   J302!=; ERASE;

     (CHECKS FOR A AND R IN BOUNDARY);
     YA17;  SHL+2;     DUP;     =RC9;   SET20;
212; DUP;   JS1P56;   (JS1L56); J51;   =M7M8Q; DC9;
     J212C9NZ;

     (INPUT: INNER BOUNDARY CONDITIONS CONDENSATION);
     ERASE; SETB41;   ZERO;     =YA15; ZERO;   =YA16;
213; SET20; JS1P55;   (JS1L55); J49;   J213!=; ERASE;
     SETB62;
     SET20; JS1P55;   (JS1L55); J49;   REV;    J214!=;
     ERASE; =RC9;
215; SET20; JS1P56;   (JS1L56); J51;   =M7M8Q; DC9;
     J215C9NZ;
     ZERO;  SET1;      =YA15;
     (INPUT INNER BOUNDARY CONDITIONS CONDENSATION COMPLETE);

     (INPUT OF THERMAL CONDITIONS);
350; SETB41;
199; SET20; JS1P55;   (JS1L55); J49;
     J199!=; (Get next none space character);
     ERASE; SET20;    JS1P55;   (JS1L55); J49;
214; SETB54;J303!=;   ERASE;    ERASE;    (CHECKS L);
     YA19;  =RC9;

     (INPUT: K, RO, C);
216; SET3;  =RC10;
217; SET20; JS1P56;   (JS1L56); J51;  (EXIT2); =M3M1Q;
     DC10;  J217C10NZ;

     (INPUT: NUMBER OF STEPS);
     JSP104;=M3M1Q;

     (INPUT: OUTER RADIUS);
     SET20; JS1P56;   (JS1L56); J51;  (EXIT2); =M3M1Q;
     M3M1Q; =M3M1Q;   (to skip over two store positions);
     DC9;   J216C9NZ;
     (THERMAL CONSTANTS INPUT.h NOT CALCULATED);

     SETB45;
     (INPUT OF OUTER BOUNDARY CONDITIONS CONDENSATION);
218; SET20; JS1P55;   (JS1L55); J49;   J218!=;  ERASE;
     SETB62;
     SET20; JS1P55;   (JS1L55); J49;   J304!=;  ERASE;
     YA18;  SHL+2;  DUP;   =RC9;    SET20;

219; DUP;   JS1P56;   (JS1L56); J51;    (EXIT2); =M7M8Q;
     DC9;   J219C9NZ; ERASE;
     (INPUT OF OUTER BOUNDARY CONDITIONS CONDENSATION COMPLETE);

     SETB45;
     (INPUT OF OUTER BOUNDARY CONDS CONVECTION);
220; SET20; JS1P55;   (JS1L55); J49;    J220!=;
     ERASE; SETB62;
     SET20; JS1P55;   (JS1L55); J49;    REV;    J221!=;
     ERASE; =RC9;
222; SET20; JS1P56;   (JS1L56); J51;    =M7M8Q;
     DC9;   J222C9NZ;

     SET1;  =YA16;  SETB45;
     (INPUT OF OUTER BOUNDARY CONDS CONVECTION COMPLETE);

223; SET20; JS1P55;   (JS1L55); J49;    J223!=; ERASE;
     SET20; JS1P55;   (JS1L55); J49;    ZERO;   REV;
221; REV;   ERASE;  SETB55; J305!=; ERASE;   M1;     =YA9;    (aNR);
     SET8;  =RM7;   YA19;   DUP;    =RC9;   (NR);    =M3M1Q;
     SET1;  DUP;    =M3M1Q; =RM6;   SET20;
     M3M7;  NOT;    NEG;    DUP;    =M3M1Q; =RC10;
     M3;    M1;     +;      =YA25;
224; DUP;   JS1P56;   (JS1L56); J51;    =M3M1Q;
     DC10;  J224C10NZ; ERASE;

     DC9;   J225C9Z;

226; M+I6;  M6;     DUP;      =M3M1Q; SET7;   xD;      CONT;
     NOT;   NEG;    =M7;      M3M7;   DUP;    =RC10;   =M3M1Q;
227; SET20; JS1P56; (JS1L56); J51;    =M3M1Q;
     DC10;  J227C10NZ;DC9;  J226C9NZ;
     (INPUT OF TEMPERATURES COMPLETE);

     (INPUT: PRINT TIME);
225; SETB02;
     M3;    M1;     +;        NEG;    NOT;    =YA26;
228; SET20; JS1P55; (JS1L55); J49;    J228!=;
     ERASE;  SETB60;
     SET20; JS1P55; (JS1L55); J49;    J306!=;
     ERASE; SETB62;
     SET20; JS1P55; (JS1L55); J49;    J306!=; ERASE;
     SET20; JS1P56; (JS1L56); J51;
     DUP;   SET20;  JS1P56;   (JS1L56); J51;
     REV;   ÷F;     FIX;      SET47;  -;      =C11;     SHAC11;
     SET2;  =RM7;   =M3M7Q;   =M3M7Q;

     (INPUT: T MAX);
     SETB41; NOT;
229; SET20; JS1P55;   (JS1L55); J49;  J229!=; ERASE;
     SET1;  =RM7;   YA21;
     DUP;   YA19;   =C7;      =YC30;

     (CALCULATE THE NODE SPACING);
230; SET7;  =+M7;
     M3M7Q; (NS);   M3M7;     DUP;    =YC31;  DUP;    REVD;
     PERM;  -F;     DUP ;     CAB;    SET47;  FLOAT;    /F;
     M-I7;  =M3M7;  J307<=Z;  YC31 ;  =YC30;  J230C7NZ;
     ERASE;


(********************************************************************);
(    VALIDATE THE DATA                                               );
(********************************************************************);
     (Check inner boundary times);
     Q1;    Q2;     Q3;     Q4;    (Save Q stores during the checks);
     YA22;  =RM1;   SET-2;  =+M1;    SET-4;   =I1;
     YA17;  DUP;    =C1;    Q1TOQ2;
     YA15;  J235=Z; SHL+2;  NEG;     =+M2;    Q2TOQ4;
     J236;

235; ERASE; Q2TOQ4;
236; M0M2Q; M0M1Q;  DUPD;   J310!=;  ERASE;   OR;      J241C1Z;
237; M0M2Q; M0M1Q;  DUPD;   J310!=;  OR;      CAB;     -F;
     J308<Z; J237C1NZ;
     (Inner boundary times checked);

241; ERASE;

     (Check outer boundary times);
242; YA17;  SHL+2;  NEG;    =+M4;   YA18;   DUP;    =C4;
     Q4TOQ3;
     YA16;  J238=Z; SHL+2;  NEG;    =+M4;   J239;

238; ERASE;
239; M0M4Q; M0M3Q;  DUPD;   J311!=; ERASE;
     OR;    J243C4Z;
240; M0M4Q; M0M3Q;  DUPD;   J311!=; OR;     CAB;   -F;
     J309<Z;J240C4NZ;
243; ERASE;

244; =Q4;   =Q3;    =Q2;    =Q1;    (Restore Q stores after the checks);
     (Outer boundary times checked);


(********************************************************************);
(    START OUTPUT OF INITIAL CONDITIONS                              );
(********************************************************************);
     (CHOOSE WHICH HEADING TO PRINT OUT);
     YA14;  J351=Z;

     (OUTPUT: "NODE RADII");
     V71;    =YC1;
     V72;    =YC2;
     SETB70; =YC0;  V76;    SET8;   OUT;
     J352;

     (OUTPUT: "DISTANCES FROM DATUM");
351; V84;   =YC1;
     V85;   =YC2;
     V86;   =YC3;
     SETB70;=YC0;   V76;    NOT;    NEG;
     SET8;  OUT;


352; Q1;      =YC31;
     Q2;      =YC30;
     Q3;      =YC29;
     Q5;      =YC28;
     Q7;      =YC27;
     Q8;      =YC26;
     DC1;     C1;      NEG;   DUP;   DUPD;   =RM7;
     SETAY0;  =RM3;
     YA9;    (aNR);    =RM1;  M3M1Q; =C2;   (Number of Regions);
     YA21;   (Inner Radius);
     M3M1Q;  (MT);    SET7;   xD;    CONT;   NOT;    NEG;  =RM5;
     M3M1Q;  (NT);    =RC8;   DUP;   =M3M7Q; M3M5;   (h);

     C8;      =+M1;
     DC8;
     J231;

232; M3M1Q;   (MT);    SET7;   xD;    CONT;   NOT;    NEG; =RM5;
     M3M1Q;   (Nt);    =RC8;   M3M5;  (h);
     C8;      =+M1;

*231; DUP;    CAB;     +F;     DUP;   =M3M7Q; REV;    DC8; J231C8NZS;
     ERASE;   DC2;     J232C2NZ;


     ERASE;   C7TOQ8;  NC8;    SETAY0; +;    =M8;    SET-1;
     =V64P99; Q8TOQ15; JS8P99; (Node radii printed out);


    (OUTPUT: "TIME TEMPERATURES");
     SETB70;  =YC0;
     V73;     =YC1;
     V74;     =YC2;
     V75;     =YC3;

     V27;     SET12;   -;     SET8;    OUT;

     (OUTPUT: INITIAL TEMERATURES);
     (Move into print area);
     =RM7;
     YA9;     =RM1;
     M3M1Q;   =C2;     (NR);
233; M+I1;    M3M1Q;   =RC8;  (Nt);
*234;M3M1Q;   =M3M7Q;  *DC8;  J234C8NZS;
     DC2;     J233C2NZ;

     C7TOQ8;  NC8;     SETAY0;+;       =M8;    SET-1;
     =V64P99; Q8TOQ15; JS8P99;(Print specified storage area);
     (INITIAL TEMPERATURES AT TIME 0 PRINTED OUT);


     YC31;    =Q1;
     YC30;    =Q2;
     YC29;    =Q3;
     YC28;    =Q5;
     YC27;    =Q7;
     (YC26;    =Q8; Missing. Why?);

     I1=+1;
     SET1;    =M1;
     C1TOQ11; NC11;

     (OUTPUT: CRLF'S);
     V8;      =V9;
     V25;     =V10;
     V26;     =V11;
     V14;     SET8;  OUT;

     (CALCULATE a/1, (b-1)/1 and c/1 TERMS FOR EACH REGION AND STORE);
     SETAY0;  =RM3;    M3M1Q; DUP;   =RC4; =M4;
     JSP36;   SET5;    =RM1;
     M4;      =C4;     V6;
8;   DUP;     M3M1Q;   xF;    M3M1Q; M3M1Q;
     xF;      M3M1Q;   DUP;   xF;    xF;
     /F;      DUP;     NEGF;  DUP;   =M3M1Q;
     REV;     V5;      xF;
     =M3M1Q;  =M3M1Q;  DC4;
     J8C4NZ;
     ERASE;
     SETAY0;  =M5;     C11;  =+M5;
     M3M1Q;   =C9;
     V6;      DUP;     =YB1; =YB2;
     ZERO;    =YA10;
     Y3;      =YA28;   JS50P32;





(********************************************************************);
(    ENTRY INTO MAIN LOOP HERE                                       );
(********************************************************************);

9;   YA21;   =V0P37;
     SET1;   =RM1;
     DC1;    JSP32;
     M3M1;   SET7;   xD;    CONT;  SET4;  +;
     DUP;    =+M1;   NEG;   =+C1;
     M3M1Q;  =C5;    ZERO;  =RM2;  M3M1Q; M3M1Q; =C6;
     SET7;   xD;     CONT;  SET2;  -;     =M4;
     JS3P37; (Polar coordinate adjustment routine.)
     (POLAR COORDINATE CORRECTION FACTOR);
     SETAY1; =V0P42;
     M5;     M2;     +;     =V2P42;
     Q1;     YA22;   =RM1;  I1=-1;
     Q1;     REV;    =Q1;   =V3P42;
     YA9;    SETAY0; +;     =V1P42;
     (INNER BOUNDARY CALCULATION);
     JSP33;  (First boundary calc.);
     SET3;   =+M2;   SET7;  =+M4;  M+I1; DC1;
     M+I2;   DC2;
     SET-3;  =+C2;

     DC6;    J10C6NZ;
     SET-1;  =+M1;   SET+1; =+C1;  J12;
10;  DC6;
     (MATERIAL CALCULATION);
11;  SET-3;  =+M4;

171; J12C6Z; JSP37;  (Adjust for polar coordinates);
     M3M4Q;  YA4;    xF;    YB1;   xF;   =M5M2Q;
     M3M4Q;  YA4;    xF;    V6;    +F;   =M5M2Q;
     M3M4Q;  YA4;    xF;    YB2;   xF;   =M5M2Q;
     M3M1Q;  =M5M2Q;
     DC6;    J11;

     (INTERFACE CALCULATION);
12;  DC5;    J13C5Z;
     JSP34;  (Interface calculation);
     J171;

13;  JSP37;  (Adjust for polar coordinates);
     M5;     M2;    +;     =V4P42;
     M3;     M1;    +;     =V5P42;

     (OUTER BOUNDARY CALCULATION);
     JSP35;  (Second boundary calc.);
     YA4;    DUP;    +F;    YA10;  +F;   =YA10;  JSP50;
     (EXPLICIT CALCULATION);
     JSP43;  (Explicit calculation routine.);
     Q14;    =V27P99;
     Q15;    =V45P99;
     Q15;    JSP1;   (Gauss Elimination);

15;  YA27;   J16!=Z;
     JSP73;  (Restore results into array.);
     (JSP98);  (Output critical values *****************************TESTING******************************);
     J9;     (Main loop start);

16;  C10TOQ8;
     DUP;    =Q15;
     SET-1;  =V64P99;
     JS8P99; (Print out store area specified by N1);
     JSP73;  (Restore results into array.);
     (JSP98);  (Output critical values *****************************TESTING******************************);

     (Get the number of ?? from Y2. Subtract 1, save no of ?? left and do next ??);
     SET2;   =RM1;    M3M1;   NEG;   NOT;  DUP; =M3M1; J9!=Z;  (Main loop);

     (Get the number of cases from Y0. Subtract 1, save no of cases left and do next case);
     ZERO;   =RM1;    M3M1;   NEG;   NOT;  DUP; =M3M1; J1!=Z;  (Next case);
20;  V8;     =V9;
     V25;    =V10;
     V26;    =V11;

     SET20;  JSP58;   (JS1L58); (Deallocate device);
     V14;    SET8; OUT;
    (JE4);  (What was this for?);
(******************************************************************************);
(    End of Program                                                            );
(******************************************************************************);
     ZERO;  OUT;


(********************************************************************);
(    FAILURE MESSAGES PRINT OUT                                      );
(********************************************************************);

50;  V8;    =V9;     V21;    =V10;
     V22;   =V11;    V14;    SET8;  OUT;
     J49;

51;  V8;    =V9;     V23;    =V10;
     V24;   =V11;    V14;    SET8;  OUT;

49;  DUP;   J48<Z;   DUP;    J47=Z; SET1;
     -;     J46=Z;   J45;

48;  V8;    =V9;
     V12;   =V10;
    V13;    =V11;
     V14;   SET8;    OUT;    J20;

47;  V8;    =V9;
     V15;   =V10;
     V16;   =V11;
     V14;   SET8;    OUT;    J20;

46;  V8;    =V9;
     V17;   =V10;
     V18;   =V11;
     V14;   SET8;    OUT;    J20;

45;  V8;    =V9;
     V19;   =V10;
     V20;   =V11;
     V14;   SET8;    OUT;    J20;

400; Q1;    Q2;
     SETAV28;=RM1;   SETAYC1; =RM2;
     SETB70; =YC0;   SET10;   =C2;
*401;M0M1Q;  =M0M2Q;  *J401C2NZS;
     SETB02; =YC11;
     V27;    SET4;    -;       SET8;   OUT;
     =Q2;    =Q1;
EXIT1;


(********************************************************************);
(    ??????????????????????????                                      );
(********************************************************************);
301; JS400;  V52;     =YC1;    V53;    =YC2;
463; V76;    J450;

302; JS400;  V39;     =YC1;
461; V40;    =YC2;
     V41;    =YC3;
     V42;    =YC4;
     V76;    SET2;    +;       J450;


303; JS400;  SETAV44; J464;
304; JS400;  V43;     =YC1;    J461;

305; JS400;  SETAV47;
464; =RM10;  M0M10Q;  =YC1;
     M0M10Q; =YC2;
     M0M10;  =YC3;
462; V76;    NOT;     NEG;     J450;

306; JS400;
     V50;    =YC1;
     SETB02; =YC2;     J463;

307; V54;    =YC1;
     V55;    =YC2;
     V56;    =YC3;
     V57;    =YC4;
     V58;    =YC5;
     V59;    =YC6;
     YC30;   JSP101;   =YC7;
     ZERO;   =YC8;
     YC31;   JSP101;   =YC8;
     SETB02; =YC9;
     V27;    SET6;     -;     J450;

310; (INNER BOUNDARY CONDITION TIMES);
     (NOT EQUAL FAULTY VALUES ARE);
     V39;    =YC1;     J465;

311; V43;    =YC1;
465; ERASE;  V40;      =YC2;
     V41;    =YC3;
     V83;    =YC4;
     V81;    =YC5;
     V82;    =YC6;
     ZERO;   NOT;      =YC7;
     V64;    =YC8;
     V65;    =YC9;
     V66;    =YC10;
     JSP101; =YC13;
     ZERO;   =YC12;
     JSP101; =YC11;
     SETB02; =YC14;
     V27;    NEG;      NOT;
     J450;
308; V39;    =YC1;
     JSP101; =YC13;
     M-I1;   M-I1;     M0M1;
     JSP101; =YC11;
     ZERO;   =YC12;    J466;

309; V43;    =YC1;
     JSP101; =YC13;
     M-I3;   M-I3;     M0M3;
     JSP101; =YC11;
     ZERO;   =YC12;
466; (BOUNDARY CONDITIONS NOT IN ASCENDING);
     (ORDER FAULTY VALUES ARE);
     V40;    =YC2;
     V41;    =YC3;
     V60;    =YC4;
     V61;    =YC5;
     V62;    =YC6;
     V63;    =YC7;
     SETB02; =YC14;
     V27;    NEG;     NOT;     (J450);

450; SETB70; =YC0;    SET8;    OUT;
     SETB41; NOT;
451; SET20;  JSP55;   (JS1L55);
     (EXIT1);J49;
     (EXIT2);J451!=;
     (Get next case to be processed.);
     ERASE;  J1;


(********************************************************************);
(    Polar coordinate adjustment routine                             );
(********************************************************************);

P37V1;
     (THIS ROUTINE EVALUATES TWO TERMS);
     (THESE ARE(R-h/2)/R and(R+h/2)/R);
     (V0=r);
4;   YA14;  J2!=Z;  (YA14 = 0 for Cartesian coordinates);

EXIT1;
2;   V0;    DUP;    DUPD;    YA3;
     V5P0;   /F;    -F;      REV;    /F;   =YB1;
     V0;    YA3;    +F;      =V0;    YA8;
     V5P0;  /F;     +F;      REV;    /F;   =YB2;
EXIT1;

3;   Y8;    DUP;    =YA3;    =YA8;    J4;

(********************************************************************);
(    boundary condition time step check routine                      );
(********************************************************************);
P60V5;
     (THIS ROUTINE ENSURES THAT THE BOUNDARY CQNDITIONS AT EACH SPECIFIED POINT ARE USED);
     V4=F2;
     Q1;     Q2;    Q3;    Q4;    (Save the Q stores);
     ZERO;   NOT;   SHL-1; DUP;   =V1;    =V3;
     YA22;   =RM1;  (aMA);
     SET-1;  =I1;
     M0M1Q;  (MA);
     M0M1Q;  =RC3;  (MB);  =C1;
     SET-4;  =I1;   Q1TOQ4;
     SET-4;  =I1;
     YA10;   DUP;   M0M1Q; SIGNF; J2<Z;  J3C1Z;
1;   DUP;    M0M1Q; SIGNF; J2<Z;  J3C1Z; J1;

2;   M-I1;   ERASE; M0M1;  =V0;   J4;

3;   ERASE;  ZERO;  NOT;   SHL-1; =V0;
4;   YA15;   J5=Z;
     YA17;   SHA+2; NEG;   =+M4;  Q4TOQ1;
     YA10;   DUP;   M0M1Q; SIGNF; J6<Z;   J7C1Z;
8;   DUP;    M0M1Q; SIGNF; J6<Z;  J7C1Z;  J8;

6;   ERASE;  M-I1;  M0M1; =V1;   J5;

7;   ERASE;  ZERO;  NOT;   SHL-1; =V1;
5;   C3TOQ4; YA17;  SHA+2; NEG;   =+M4;   Q4TOQ1;
     YA10;   DUP;   M0M1Q; SIGNF; J9<Z;   J10C1Z;
17;  DUP;    M0M1Q; SIGNF; J9<Z;  J10C1Z; J17;

9;   M-I1;   ERASE; M0M1;  =V2;   J11;

10;  ERASE; ZERO;   NOT;   SHL-1; =V2;
11;  YA16;  J12=Z;
     YA18;  SHA+2;  NEG;   =+M4;  (No of locations used by outer boundary conditions);
     Q4TOQ1; YA10;
     DUP;   M0M1Q;  SIGNF; J13<Z; J14C1Z;
15;  DUP;   M0M1Q;  SIGNF; J13<Z; J14C1Z;  J15;

13;  ERASE; M-I1;   M0M1;  =V3;   J12;

14;  ERASE; ZERO;   NOT;   SHL-1; =V3;
12;  V0;    V1;
     MAXF;  ERASE;  V2;
     MAXF;  ERASE;  V3;
     MAXF;  ERASE;  YA10;
     -F;    V4;     /F;    =V5;  (1 BOUNDARY MAX);
     =Q4;   =Q3;    =Q2;   =Q1;  (Restore the Q stores);

EXIT1;

(********************************************************************);
(     TEST ROUTINE - Used to output intermediate values during run.  );
(********************************************************************);
P98V66;
     (V0=stream number);
     V1  = B 4066 2060 2520 0000; (V1 =P V0P50);
     V3  = B 4066 2160 2520 0000; (V3 =P V1P50);
     V5  = B 4066 2260 2520 0000; (V5 =P V2P50);
     V7  = B 4066 2360 2520 0000; (V7 =P V3P50);
     V9  = B 4066 2126 6023 2200; (V9 =P V16P32);
     V11 = B 4066 2127 6023 2200; (V11=P V17P32);
     V13 = B 4066 2220 6023 2200; (V13=P V20P32);
     V15 = B 4066 2221 6023 2200; (V15=P V21P32);
     V17 = B 4066 2760 2323 0000; (V17=P V7P33);
     V19 = B 0202 0200 0000 0000; (V19 = P[7DC]);   (TOO MUCH SPACING. REMOVED 4 LF);

     (V20=stream number);
     V21=PV8P33;
     V23=PV8P35;
     V25 = B 4045 7060 6421 4000; (V25=PEXP.T1.);
     V27 = B 4045 7060 6456 4000; (V27=PEXP.Tn.);
     V29 = B 4064 5155 4540 0000; (V29=PTIME );
     V31 = B 4064 0063 4000 0000; (V31=PT.S. );
     V33 = B 4064 0054 4546 6440; (V33=PT.LEFT );
     V35 = B 4060 0064 0055 4000; (V35=PP.T.M. ); (fixed point);
     V37 = B 4066 2224 6023 2240; (V37=PV24P32 );
     V39 = B 0202 0200 0000 0000; (V39=P[7DC]);    (TOO MUCH SPACING. REMOVED 4 LF);

     V40 = Q0/AV0/AV19;
     V41 = Q0/AV20/AV39;
     (V42= stream number);
     V43 = PV7P35;
     V45 = B 0202 0200 0000 0000; (V45=P[7DC]);    (TOO MUCH SPACING. REMOVED 4 LFS);

     V46 = Q0/AV42/AV45;
     (V47= stream number);
     V48 = Pa1;
     V50 = Pb1;
     V52 = Pc1;
     V54 = PT1;
     V56 = B 4041 5640 0000 0000; (V56 = Pan );
     V58 = B 4042 5640 0000 0000; (V58 = Pbn );
     V60 = B 4043 5640 0000 0000; (V60 = PCn );
     V62 = B 4064 5640 0000 0000; (V62 = PTn );
     V64 = B 0202 0202 0000 0000; (V64 = P[7DC]);  (TOO MUCH SPACING. REMOVED 3 LFS);

     V65=Q0/AV47/AV64;

     SETB30; DUP ;   =V0; DUP ; =V20; =V42;

     (OUTPUT: Key values from P50 and P32);
     V0P50;  JSP101; =V2; (Convert floating point);
     V1P50;  JSP101; =V4;
     V2P50;  JSP101; =V6;
     V3P50;  JSP101; =V8;
     V16P32; JSP101; =V10;
     V17P32; JSP101; =V12;
     V20P32; JSP101; =V14;
     V21P32; JSP101; =V16;
     V7P33;  JSP101; =V18;
     V40;    SET8;   OUT;

     (OUTPUT: Key values from P33 and P35);
     V7P35;  JSP101; =V44;
     V46;    SET8;   OUT;

     V8P33;  JSP101; =V22;
     V8P35;  JSP101; =V24;
     Q1;
     YA25;   =M1;    M0M1;   JSP101; =V26;
     YA26;   =M1;    M0M1;   JSP101; =V28;
     =Q1;
     YA10;   JSP101; =V30;
     YA4;    JSP101; =V32;
     YA28;   JSP101; =V34;
     YA27;   JSP100; =V36;
     V24;    JSP101; =V38;
     V41;    SET8;   OUT;

     SETB30; =V47;   V65;     SET8;   OUT;
EXIT1;

(********************************************************************);
(     FLOATING POINT CONVERSION ROUTINE                              );
(********************************************************************);
P105V3;
     V0=B 2037 2020;
     V1=F100;
     V2=B 3600 0000 0000 0000;
     Q13;    =V3;
     STAND;  DUP;    ABSF;    V1;     xF;
     FIX;    SET47;  -;       =C13;   SHAC13;
     V3; =Q13;
     JSP100; ZERO;   SHLD+36; SHL+6;
     SHLD-36;ERASE;  V0;      OR;
     REV;  J1<Z;
EXIT1;
1;   V2;   OR;
EXIT1;

(********************************************************************);
(     BOUNDARY ROUTINE. INTERPOLATION.                               );
(********************************************************************);
P50V7;
     (V0=Tamb a1);
     (V1=Tamb a2);
     (V2=2/h+1/R OUTER);
     (V3=2/h-1/R INNER);
     (V4=h INNER);
     (V5=h OUTER);
     YA4;     DUP;
     JS50P32; (Get the time step);
     V26P32;  xF;
     YA12;    /F;     V4;     xF;
     V3;      xF;     V27P32; xF;   =V0;
     V28P32;  xF;     YA13;   /F;   V5;  xF;
     V2;      xF;
     V29P32;  xF;     =V1;
     ZERO;    =V3;    ZERO;   =V2;
EXIT1;


(********************************************************************);
(     CALCS K/pch^2 MAX.  and 1 MAX                                   );
(********************************************************************);
P36V1;
     V0 = F2;
     V1 = F1;
     Q1;     Q2;     Q3;      Q4;     (Save the Q stores);
     YA19;   =RC4;   SETAY0;  =RM1;   SET5; =RM2;
     M1M2Q;  M1M2Q;  M1M2Q;   xF;     M1M2;   xF;
     DUP;    =YA12;  M1M2Q;   xF;     /F;
     DUP;    =M1M2Q; DUP;     DUP;    +F;     =M1M2Q;
     DUP;    =M1M2Q; DC4;     J197C4Z;DC4;
     J196C4Z;
8;   M1M2Q;  M1M2Q;  /F;      M1M2Q;  /F;
     M1M2Q;  DUP;    xF;      /F;     DUP;    =M1M2Q; DUP;
     DUP;    +F;     =M1M2Q;  DUP;    =M1M2Q; MAXF;
     REV;    ERASE;  DC4;     J8C4NZ;
196; M1M2Q;  M1M2Q;  M1M2Q;   xF;     M1M2;   xF;
     DUP;    =YA13;  M1M2Q;   xF;     /F;     DUP;
     =M1M2Q;  DUP;    DUP;     +F;     =M1M2Q; DUP;
     =M1M2Q; MAXF;   REV;     ERASE;
     J198;
197; YA12;   =YA13;
198; DUP;    V0;     xF;      V1;     REV;     /F;   =YA23;
     =YA24;
     =Q4;    =Q3;    =Q2;     =Q1;    (Restore the Q stores);
EXIT1;

(********************************************************************);
(     EXPLICIT CALCULATION ROUTINE                                   );
(********************************************************************);
P43V6;
     V6=F1;
     SET-4;  DUP;    =+C2;    NEG;    =+M2;
     M2;     =RC15;  M5TOQ15; SET2;   =M2;   NC2;
     M5M2;   (c);    M+I2;    DUP;    V8P33; NEGF;  +F;    M5M2; (k)
     DUP;    CAB;    V6;      +F;     xF;    SET4;
     =+M2;   CAB;    M5M2;   xF;     NEGF; +F;
     V7P33;  +F;     V0P50;  +F;
     SET-4;  =+M2;   =M5M2;  M+I2;
     SET-8;  =+C2;
14;  M5M2;   DUP;    NEGF;   CAB;    xF;   REV;   M+I2; M+I2;
     M5M2;   DUP;    PERM;   +F;     V6;   +F;    M+I2; M5M2;  DUP;
     PERM;   xF;     CAB;    SET4;   =+M2; M5M2;  xF;   NEGF;
     +F;     CAB;    +F;     SET-4;  =+M2; =M5M2;
     M+I2;   SET-4;  =+C2;
     J14C2NZ;
     M5M2;   DUP;    NEGF;   CAB;    xF;   REV;
     V8P35;  NEGF;   +F;
     SET3;   =+M2;   M5M2;   DUP;    CAB;
     xF;     +F;     +F;     V7P35;  +F;   V1P50; +F;   =M5M2;
EXIT1;


(********************************************************************);
(     RESTORE ROUTINE                                                );
(********************************************************************);
P73;
     SETAY0; =RM1;
     =Q6;    YA9;    =RM1;   M3M1Q; =C5;
1;   M+I1;   M3M1Q;  =RC4;
*2;  M0M6Q;  =M3M1Q; DC4;   J2C4NZS;
     DC5;    J1C5NZ;
EXIT1;

(********************************************************************);
(     INTERFACE BETWEEN MATERIAL PHASES CALCULATION                  );
(********************************************************************);
P34V6;
     V5=F2;
     V6=F1;
     M3M1Q; (Interface temp);
     M3M1Q; (Material type);
     M3M1Q; (No.of nodes);
     =C6;
     DC6;
     SET7;   xD;     CONT;   SET2;   -;    =M4;
     M3M4Q;  =YA5;
     M3M4Q;  =YA6;
     M3M4Q;  =YA7;
     M3M4Q;  =YA8;
     J3P37;  (Polar coordinates adjustment);
     V5;     YA1;    YA2;    xF;     YA6;
     YA7;    xF;     +F;     /F;
     YA4;    YA3;    YA8;    +F;     V5;
     /F;     /F;     xF;
     DUP;    DUP;    YA0;    YA3;    /F;
     xF;     NEGF;   YB1;    xF;     =M5M2Q;
     YA0;    YA3;    /F;
     YB1;    xF;     YA5;    YA8;    /F;
     YB2;    xF;     +F;
     xF;     V6;     +F;     =M5M2Q; YA5;
     YA8;    /F;     xF;
     NEGF;   YB2;    xF;     =M5M2Q;
     YA5;    =YA0;
     YA6;    =YA1;
     YA7;    =YA2;
     YA8;    =YA3;
     =M5M2Q; (store interface temp);
EXIT1;


(********************************************************************);
(     TIME STEP ROUTINE                                                );
(********************************************************************);
P32V30;
     V0/1=PCHANGEOVER*T0*[2D];
     V2/3=PCONDENSATIDN*[3D];
     V4/5=PCONVECTION*[5D];
     V6=PAT*TIME*;
     (V7/V13 = Q1/Q7);
     (V14=HV15=Tamb Outer B condensation);
     (V16=HVI7=Tamb Outer B);
     (V18=HV19=Tamb Inner B condensation);
     (V20=HV21=Tamb Inner B);
     V22=F10;

     Q1;     =V7;
     Q2;     =V8;
     Q3;     =V9;
     Q4;     =V10;
     Q5;     =V11;
     Q6;     =V12;
     Q7;     =V13;
     SET8;   =RC3;
    SETAV14; =M3;
     ZERO;   =YA27;
     J51;

50;  VR;     (Clear overflow);
     Q1;     =V7;
     Q2;     =V8;
     Q3;     =V9;
     Q4;     =V10;
     Q5;     =V11;
     Q6;     =V12;
     Q7;     =V13;
     SET8;   =RC3;
    SETAV14; =M3;

*8;  ZERO;   =M0M3Q; *J8C3NZS; (Clear working storage);
     YA17;   =RC6;
     YA22;   =RM5;   (aMA);
     M-I5;   M-I5;   SET-4;  =I5;    Q5;
     JS3;
     V14;    =V20;   V15;    =V21;
     YA17;   SHA+2;  NEG;    +;      =Q5;
     YA15;   J16=Z;  Q5;     YA17;   =RC6;
     JS3;
     V14;    =V18;   V15;    =V19;
     YA17;   SHA+2;  NEG;    +;      =Q5;
     YA25;   =RM1;
     V21;    M0M1;   -F;     V20;    xF;   (cond);
     V19;    M0M1;   -F;     V18;    xF;   (conv);
     MAXF;   ERASE;  ERASE;  J14NV;  (Jump no overflow);
     J15;

14;  V20;    =V26;
     V21;    =V27;
     V18;    =V20;
     V19;    =V21;
     YA30;   J16!=Z;
     SET1;   =YA30;
     SET1;   =YA20;

     (OUTPUT: "INNER BOUNDARY CONDITIONS");
     SETB70; =YC0;
     V39P0;  =YC1;
     V40P0;  =YC2;
     V41P0;  =YC3;
     V42P0;  =YC4;
     V76P0; (Q0/AYC0/AYC2);  SET2;   +;      SET8;   OUT;

     (OUTPUT: "CHANGEOVER*TO*CONVECTION*AT*TIME");

     SETB70; =YC0;
     V0;     =YC1;
     V1;     =YC2;
     V4;     =YC3;
     V5;     =YC4;
     V6;     =YC5;
     YA10;   JSP101; =YC6;
     SETB02; =YC7;
     V76P0;  SET5;  +;
     SET8;   OUT;
     J7;

15;  YA30;   J16=Z;
     V18;    =V26;
     V19;    =V27;
     ZERO;   =YA30;
     SET1;   =YA20;

     (OUTPUT: "INNER BOUNDARY CONDITIONS");
     SETB70; =YC0;
     V39P0;  =YC1;
     V40P0;  =YC2;
     V41P0;  =YC3;
     V42P0;  =YC4;
     V76P0;  SET2;   +;      SET8;   OUT;


     (OUTPUT: "CHANGEOVER*TO*CONDENSATION*AT*TIME");
     SETB70; =YC0;
     V0;     =YC1;
     V1;     =YC2;
     V2;     =YC3;
     V3;     =YC4;
     V6;     =YC5;
     YA10;   JSP101; =YC6;
     SETB02; =YC7;
     V76P0;  SET5; +;
     SET8;   OUT;
     J7;

16;  V20;    =V26;
     V21;    =V27;
     ZERO;   =YA20; (NO CHANGEOVER);

7;   YA18;   =RC6;
     Q5;
     JS3;
     V14;    =V16;
     V15;    =V17;  =Q5;
     ZERO;   =V14;
     ZERO;   =V15;
     YA16;   J19=Z;
     YA18;   SHA+2;  NEG;  =+M5;
     YA18;   =RC6;
     JS3;
     YA26;   =RM1;
     V17;    M0M1;   -F;     V16;    xF;    (cond);
     V15;    M0M1;   -F;     V14;    xF;    (conv);
     MAXF;   ERASE;  ERASE;  J17NV;
     J18;

17;  V16;    =V28;
     V17;    =V29;
     V14;    =V16;
     V15;    =V17;
     YA31;   J19!=Z;
     SET1;   =YA31;
     SET1;   =YA29;

     (OUTPUT: "OUTER BOUNDARY CONDITIONS");
     SETB70; =YC0;
     V43P0;  =YC1;
     V40P0;  =YC2;
     V41P0;  =YC3;
     V42P0;  =YC4;
     V76P0;  SET2;   +;
     SET8;   OUT;

     (OUTPUT: "CHANGEOVER TO CONVECTION AT TIME");
     SETB70; =YC0;
     V0;     =YC1;
     V1;     =YC2;
     V4;     =YC3;
     V5;     =YC4;
     V6;     =YC5;
     YA10;   JSP101; =YC6;
     SETB02; =YC7;
     V76P0;  SET5;   +;
     SET8;   OUT;
     J10;

18;  YA31;   J19=Z;
     V14;    =V28;
     V15;    =V29;
     ZERO;   =YA31;
     SET1;   =YA29;

     (OUTPUT: "OUTER BOUNDARY CONDITIONS");
     SETB70; =YC0;
     V43P0;  =YC1;
     V40P0;  =YC2;
     V41P0;  =YC3;
     V42P0;  =YC4;
     V76P0;  SET2;   +;
     SET8;   OUT;

     (OUTPUT: "CHANGEOVER*TO*CONDENSATION*AT*TIME");
     SETB70; =YC0;
     V0;     =YC1;
     V1;     =YC2;
     V2;     =YC3;
     V3;     =YC4;
     V6;     =YC5;
     YA10;   JSP101; =YC6;
     SETB02; =YC7;
     V76P0;  SET5;   +;
     SET8;   OUT;
     J10;

19;  V16;    =V28;
     V17;    =V29;
     ZERO;   =YA29;

10;  V16;    YA13;   /F;     (OUTER CONDS);
     V20;    YA12;   /F;     (INNER CONDS);
     MAXF;   REV;    ERASE;  VR;    =V24;   ZERO;  J24;


51;  YA28;
     V24;    YA24;   MAXF;   REV;   ERASE;
     V6P0;   V5P0;   /F;     REV;   /F;
     V25;    DUP;    +F;     DUP;   J20=Z;
     MAXF;
20;  ERASE; DUP;   =V25;

     (1 IS THE MAXIMUM ALLOWABLE FOR STABLE CONDITIONS AND FOR dl/dt<=2);
     JSP60; (Check boundary condition time step);
     V5P60; MAXF;  ERASE;

     (ENSURES THAT BOUNDARY CONDITIONS ARE FOLLOWED);
     DUP;   +F;    YA28;  REV;  /F;
     FIX;   SET47; -;     =C1;  SHLC1;
     NOT;   NEG;   DUP;   SET47;
     FLOAT; YA28;  DUP;   CAB;  /F;    DUP;
     V5P0;  /F;    =YA4;  -F;   =YA28; NEG; NOT;
     (CHECKS THAT TL>=CHOSEN TIME STEP);
     J24>Z;
     V5P0;  /F;    =YA4;
     SET1;  =YA27;
     Y3;    =YA28;
     ZERO;
     (the time left is equal to 1 which is the time taken for one);
     (time step is. an exp. and implicit calculation together);
24;  ERASE;
     V7;    =Q1;
     V8;    =Q2;
     V9;    =Q3;
     V10;   =Q4;
     V11;   =Q5;
     V12;   =Q6;
     V13;   =Q7;
EXIT1;

     (Note V20 and V16 are zero if K=0);
3;   YA10;  M0M5Q; DUPD;  SIGNF; J2<=Z;
     DC6;   J2C6Z;
1;   ERASE; M0M5Q; DUPD;  SIGNF; J13<=Z;
     DC6;   J2C6Z; J1;

13;  J12=;
     ERASE; SET8;  =+M5;
     M0M5Q; (T1);
     DUP;   YA10;  (T2);
     -F;    (T1-T2);
     REV;   M0M5;  (T3);
     -F;    (T1-T3);
     /F;    ((T1-T2)/(T1_T3));
     DUP;
     SET3;
     SET1;  =C6;
     =+M5;  M0M5Q;  (H1);  DUP;    M0M5;    -F;   (H1-H3);
     CAB;   xF;     -F;    NOT;    NEG;     =V14;
     J11;

2;   ERASE;
12;  ERASE; SET3;   =+M5;  ZERO;   =C6;
     M0M5;  NOT;    NEG;   =V14;
11;  J6C6Z; SET3;   =+M5;  M0M5Q;  DUP;     M0M5;
     -F;    CAB;    xF;    NEGF;    +F;
     J9;

6;   SET-1; =+M5;   M0M5;
9;   =V15;  (Tamb); SET-1; =+M5;
     M0M5;  J4=Z;
EXIT1;

4;   ZERO;  =V14;
EXIT1;   (alter);

(********************************************************************);
(     INNER/FIRST BOUNDARY                                           );
(********************************************************************);
P33V8;
     V6=F1;
     (V7=Tamb 2Hl/pch on exit V8=2Hl/pch);
     YA14;   J3=Z;  ( 1 if polar coordinates);
     ( INNER BOUNDARY ZERO RADIUS );
     ( the boundary is an insulated one therefore set H=0 R=1);
     ( To eliminate heat flux from crossing the inner boundary);
     YA21;   J3!=Z;
     ZERO;   =V20P32;
     V6;     =V0P37;
3;   Q1;     =V1;
     Q2;     =V2;
     Q3;     =V3;
     Q4;     =V4;
     V2P42;  =RM2;   (first matrix position);
     V1P42;  (aNR);  NOT;   NEG;     =RM1;   (aMT);
     M0M1Q;  SET7;   xD;    CONT;    SETAY-2; +;    =M4;
     M0M4Q;  =YA0;
     M0M4Q;  =YA1;
     M0M4Q;  =YA2;
     M0M4Q;  =YA3;
     M+I1;   V6;
     V20P32; DUP;    J1=Z;   (If the inner Tamb = 0 );
     ZERO;   =M0M2Q; (a);
     YA4;    xF;     YA12;   /F;    YA3;    xF;
     V6;     DUP;    +F;     YA3;   /F;     V6;
     V0P37;  YA3;    -F;     /F;    -F;     DUP;  =V3P50;  ( 1/h - 1/R INNER);
     xF;     DUP;    =V8;    (2Hl/pch);
     DUP;
     V21P32; xF;     =V7;    (Tamb*2Hl/pch);
     YA0;    YA4;    xF;     YA12;   /F;
     YA3;    /F;     DUP;    +F;    DUP;
     PERM;   +F;     CAB;    +F;    =M0M2Q; NEGF; =M0M2Q;
     M0M1Q;  =M0M2Q;
     J2;

1;   =M0M2Q; =M0M2Q;
     ZERO;   =M0M2Q;
     V21P32; =M0M2Q;
     ZERO;   =V7;
     ZERO;   =V8;
     M0M1Q;  ERASE;
2;   YA3;    =V4P50;
     V2P42;  =RM2;
     M0M2Q;  JSP101; =V49P98;
     M0M2Q;  JSP101; =V51P98;
     M0M2Q;  JSP101; =V53P98;
     M0M2Q;  JSP101; =V55P98;
     V1;     =Q1;
     V2;     =Q2;
     V3;     =Q3;
     V4;     =Q4;
     YB1;    =V1;
EXIT1;

(********************************************************************);
(     OUTER/SECOND BOUNDARY                                          );
(********************************************************************);
P35V8;
     V6=F1;
     (V7=Tamb 2Hl/pch on exit V8=2hl/pch);
     Q1;     =V1;
     Q2;     =V2;
     Q3;     =V3;
     Q4;     =V4;
     V4P42;  =RM2; (next matrix position);
     V5P42;  =RM1; (address of next temperature);
     V6;
     V16P32; DUP;
     Y1;     NEG;    NOT;    J3!=Z;
     YA0;    =YA5;
     YA1;    =YA6;
     YA2;    =YA7;
     YA3;    =YA8;
3;   J1=Z;
     YA5;    YA4;    xF;     YA13;    /F;    YA8;  /F;
     DUP;    +F;     DUP;
     NEGF;   =M0M2Q; REV;    YA4;     xF;    YA13; /F;
     YA8;    xF;     V6;     DUP;     +F;    YA8;
     /F;     V6;
     V0P37;  YA8;    -F;     /F;      +F;    DUP;  =V2P50; xF;
     DUP;    =V8;    DUP;    V17P32;  xF;    =V7;
     +F;     +F;     =M0M2Q; ZERO;    =M0M2Q;
     M0M1Q;  =M0M2Q;
     J2;

1;   =M0M2Q; =M0M2Q; ZERO ;  =M0M2Q;
     V17P32; =M0M2Q; ZERO;   =V7;     ZERO;  =V8;
     M0M1Q;  ERASE;
2;   YA8;    =V5P50; V4P42;  =RM2;
     M0M2Q;  JSP101; =V57P98;
     M0M2Q;  JSP101; =V59P98;
     M0M2Q;  JSP101; =V61P98;
     M0M2Q;  JSP101; =V63P98;
     V1;     =Q1;
     V2;     =Q2;
     V3;     =Q3;
     V4;     =Q4;
     YB2;    =V1;
EXIT1;


(********************************************************************);
(     ???????????????                                                );
(********************************************************************);
P104;
     SET20; JS2P56;  (JS2L56); J50P0;
     SET47; -;       =RC11;    SHAC11;
EXIT1;


(********************************************************************);
(     GAUSS ELIMINATION                                              );
(********************************************************************);
P1V5;
     V0=Q2/1/AV1;
     V1/2=PGAUSS*ELIM*FAIL[C];
     V3/4=POFLOW*IN*SOLN*[DC];
     V5=Q2/1/AV3;

     ZERO;    J6NV;      NOT;
     (TO PRESERVE THE OVERFLOW REGISTER);
6;   REV;
5;   =Q15;    (Q15=4N/1/LOWEST STORE ADDRESS OF MATRIX);
     Q0TOQ14; IM15TOQ14;

     (CHECKS C15 TO BE 4N);
     C15;     SET4;
     /I;      J3>Z;      (Error);
     ERASE;   ZERO;      ZERO;  (gandw);


1;   (FORWARD CALCULATION);
     M0M15Q;  (a);       DUP;   PERM;
     xF;      PERM;      xF;    M0M15Q;  (b);
     REV;     -F;        DUP;   M0M15Q;  (c);
     REV;     /F;        DUP;   =M0M14Q; (w);
     PERM;    M0M15Q;    (k);   CAB;     -F;
     REV;     /F;        DUP;   =M0M14Q; (g);
     J1C15NZ; ERASE;     ERASE;
     NC14;    C14;       SHA-1; =C14;    I14=-1;
     M+I14;  IM14TOQ15;   M0M14Q;  (z);  M+I15;
     I14=-2; M+I14;
*2;  M0M14;  (w);   xF;   M0M14QN; (g);  REV;   -F; DUP; =M0M15Q; (z);
     J2C14NZS;
     ERASE;  DC15;  NC15; J4V;     SHA+48;
     ERASE;
     I15=+1; M+I15; Q15;
EXIT1;

3;   V0;
     JP99;   (Print out q stores etc. Error exit);
EXIT1;

4;   V0; SET2; +;
     JP99;   (Print out q stores etc. Error exit);
EXIT1;


P42V10; (V stores used by P33);


(********************************************************************);
(       This routine performs a number of output types               );
(       J8P99 - outputs a line of values.                            );
(       J8P99 - outputs a line of values.                            );
(       This was used in the absence of external tracing capability  );
(********************************************************************);
P99V64; (failure routine);
     V23/25=P***TIME****TEMPERATURES[C];
     V26=Q0/AV0/AV3;
     (V27=Q14);
     V28=P[7DC];
     V29=B3600000000000000;
     V30=B1212121212121212;
     V31=B2020202020202020;
     V32/33=PFAILURE*OUTPUT [C];
     V34/35=PQ*STORE*CONTS [DC];
     V36/37=PNESTING*STORES[DC];
     V38/39=PEMPTY*NEST.[4DC];
     V40/41=PEMPTY*SJNS.[4DC];
     V42/43=PWORDS*AS*FROM*E*;
     V44=P[8D];
     (V45=Q15);
     (V46/61=    );
     (V62=Q12);

     (V64=marker for print out:if=-1 then do normal headings);
     (If=0 then do failure headings);
     Q15; =V45; Q14; =V27;
     Q15; (QN/1/lowest address of array);
     SETAV1; =RM14; I15=+1;

     (OUTPUT: "FAILURE*OUTPUT");
     SETB70;    =V0;
     V32;     =V1;
     V33;     =V2;
     V28;     =V3;
     DUP;     SET8;    OUT;


(********************************************************************);
(     PRINT NESTING STORES                                           );
(********************************************************************);
     (OUTPUT: "NESTING*STORES");
     SETB70;  =V0;
     V36;     =V1;
     V37;     =V2;
     SET8;    OUT;

     J130EN;  (If the nest is empty then Q STORE PRINT OUT);

     SETAV46; Q12;     =V62;   =RM12;
2;   DUP;     =M0M12Q; DUP;    JSP100; =V1;
     ZERO;    =V2;     DUP;    JSP101; =V3;
     SET3;    Q13;     =W31;   =RC13;
1;   ZERO;    SHLD+16; SHL+32; SHA-32; JSP100;
     ZERO;    SHLD+6;  REV;    SHL+6;  REV;
     SHLD-6;  OR;      REV;    DC13;   J1C13NZ;
     ERASE;   =V7;     SETB17; OR;     =V6;
     SETB17;  OR;      =V5;    ZERO;   =V4;
     SETB70;  =V0;     V28;    =V8;
     V26;     SET5;    +;      SET8;   OUT;
     W31;     =Q13;
     ZERO;    = V7;
     J3EN;
     J2;
3;   I12=-1;  M+I12;   NC12;
*4;  M0M12Q; *J4C12NZS; (RESTORE ITEMS TO NEST);
     J5;

     (OUTOUT: "EMPTY*NEST.");
130; SETB70;  =V0;
     V38;     =V1;
     V39;     =V2;
     V26;     SET8; OUT;

(********************************************************************);
(     PRINT Q STORES                                                 );
(********************************************************************);
     (OUTOUT: "Q*STORE*CONTS");
5;   SETB70;  =V0;
     V34;     =V1;
     V35;     =V2;
     V28;     =V3;
     V26;     DUP;     SET8;   OUT;
     =V4;
     Q1;      JSP102;
     Q2;      JSP102;
     Q3;      J5P102;
     Q4;      JSP102;
     Q5;      JSP102;
     Q6;      JSP102;
     Q7;      JSP102;
     Q8;      JSP102;
     Q9;      JSP102;
     Q10;     JSP102;
     Q11;     JSP102;
     V62;     JSP102;
     Q13;     JSP102;
     V27;     JSP102;
     V45;     JSP102;

(********************************************************************);
(     PRINT SJNS                                                     );
(********************************************************************);
     J9EJ;    (SJNS PRINT CUT);
     V26;     V41;     V40;      SHLD+36; SHLD-36;
     =V1;     =V2;     V28;      =V3;
     SETB70;  =V0;     SET8;     OUT;
     SETAV46; =RM12;
6;   LINK; DUP; =M0M12Q;
     ZERO;    SHLD+35; REV;      SHL-35;
     JSP100;  =V3;
     JSP100;  =V1;
     ZERO;    =V2;
     SETB70;  =V0;
     V28;     =V4;
     V26;     NOT;     NEG;
     SET8;    OUT;     J30EJ;    J6;

30;  I12=-1;  M+I12;   NC12;

*7;  M0M12Q;  =LINK;   *J7C12NZS;
     J8;

     (OUTPUT: "EMPTY*SJNS.");
9;   SETB70;  =V0;
     V28;     =V3;
     V40;     =V1;
     V41;     =V2;
     V26;     SET8;    OUT;

(********************************************************************);
(     PRINT OUT STORE AREA SPECIFIED ON ENTRY IN N1                  );
(********************************************************************);
8;   Q1;      =V46;
     V64;     J18<Z;
     ZERO;    =RM14;   C15TOQ14; C15;
     NC15;    JSP100;  =V1;
     V42;     =V2;
     V43;     =V3;
     M15;     JSP100;  =V4;
     V28;     =V5;
     SETB70;  =V0;
     V26;     SET2;    +;
     SET8;    OUT;     J10;

18;  ZERO;    =RM14;   C15TOQ14; NC15;
     YA10;    JSP101;  =V1;
     ZERO;    =V2;
     V28;     =V3;
     SETB70;  =V0;
     V26;     SET8;    OUT;


10;  SET20;   =RC1;
     SETAV1;  =M1;
     SETB77;
*27; DUP;     =M0M1Q;  *J27C1NZ;
     ERASE;

19;  SET10;   =+C15;   C15;      DUP;    J11>Z;
     ERASE;
     SET2;    =RI1;
     SET10;   =C1;
20;  M15M14Q; JSP105;  =V2M1Q;   J20C1NZ;
     V28;     =V21;
     SETB70;  =V0;
     SET18;   V26;     +;        SET8;     OUT;
     J19;

11;  SET10;   -;       J16=Z;
     M15M14Q; JSP105;  =V2;      J12C14Z;
     M15M14Q; JSP105;  =V4;      J13C14Z;
     M15M14Q; JSP105;  =V6;      J14C14Z;
     M15M14Q; JSP105;  =V8;      J21C14Z;
     M15M14Q; JSP105;  =V10;     J22C14Z;
     M15M14Q; JSP105;  =V12;     J23C14Z;
     M15M14Q; JSP105;  =V14;     J24C14Z;
     M15M14Q; JSP105;  =V16;     J25C14Z;
     M15M14Q; JSP105;  =V18;     J26;
12;  ZERO;    =V4;
13;  ZERO;    =V6;
14;  ZERO;    =V8;
21;  ZERO;    =V10;
22;  ZERO;    =V12;
23;  ZERO;    =V14;
24;  ZERO;    =V16;
25;  ZERO;    =V18;
26;  ZERO;    =V20;
     ZERO;    =V11;
     V28;     =V21;
     SETB70;  =V0;
     SET18;   V26;     +;       SET8;    OUT;
16;  V46;     =Q1;
     V45;     =Q15;
     V27;     =Q14;
     V64;     J20P0=Z;
     ZERO;    =V64;


 EXIT1;
(End of P99V64 failure routine);

(********************************************************************);
(       Q STORES PRINT OUT ROUTINE                                   );
(********************************************************************);
P102V1;
     (typical print-out is of the form -1/1/-308);
     V0=Q0/AW1/AW5;
     V1=P[7DC];
     Q13;     =W0;
     SET3;    =RC13;
1;   ZERO;    SHLD+16;SHL+32;   SHA-32;
     JSP100;  ZERO;   SHLD+6;   REV;    SHL+6;
     REV;     SHLD-6; OR;       REV;
     DC13;    J1C13NZ;

     ERASE;   =W4;
     SETB17;  OR;     =W3;
     SETB17;  OR;     =W2;
     V1;      =W5;
     SETB70;  =W1;
     V0;      SET8;   OUT;
     W0;      =Q13;
EXIT1;

(********************************************************************);
(       FLOATING POINT CONVERSION ROUTINE                            );
(********************************************************************);
P101V3;
     V0=B2037202020;
     V1=F1000;
     V2=B3600 0000 0000 0000;
     Q13;     =V3;
     STAND;   DUP;    ABSF;    V1;      xF;
     FIX;     SET47;  -;       =C13;    SHAC13;
     V3;      =Q13;
     JSP100;  ZERO;   SHLD+30; SHL+6;
     SHLD-30; ERASE;  V0; OR;
     REV;     J1<Z;
EXIT1;

1;   V2; OR;
EXIT1;

(********************************************************************);
(                                                                    );
(********************************************************************);
P100V3;
     V0=B1212121212121212;
     V1=B2020202020202020;
     V2=B3600000000000000;
     Q12;     =V3;
     DUP;     J3>=Z;  V2;      REV;    ABS;
     J4;
3;   ZERO;    REV;
4;   V0;      REV;    FRB;     SET8;   =C12;
     ZERO;
*1;  SHLD+6;  DUP;    J2!=Z;   DC12;   J1C12NZS;
2;   SHLD-6;  ERASE;  V1;     OR;     C12;
     SET8;    -;      SET6;   xD;     CONT;   =C12;
     SHLC12;  SETB20; OR;     OR;     V3;     =Q12;
EXIT1;


(********************************************************************);
(       Library Routines added to the program.                       );
(********************************************************************);
P54V3; (L54V3); (INITIALISE, 7.8.63);
     V3=0;
1;   SHL+14; ZERO;    SHLD+2; =M15;   V0M15;  J3!=Z;
     SET2;   SET5;    OUT;    REV;
4;   ZERO;   SHLD+16; =M14;    REV;    SHC-16; DUP;    M0M14;
     +;      REV;     M0M14N; +;      CAB;    SHC+16; =RM14;
     =M0M14N;=M0M14;  M14;    =V0M15;
     ZERO;   =RC15;   Q15;    =E2M14; SET8;   =C14;
*5;  ZERO;   =E3M14Q; *J5C14NZS;
EXIT1;
2;   SHL+14; ZERO;    SHLD+2; =M15;   V0M15;  J4=Z;   ERASE;
3;   ERASE;
EXIT1;
(END OF L54);

(********************************************************************);
(       FETCH CHARACTER,14.9.64                                      );
(********************************************************************);
P55V2; (L55V2);
     V0=3/7;
     V1=5/15;
     V2=B7575757575757575;
1;   SHC-2;  SHL-46; =Q15;     V0P54M15;(V0L54M15);  DUP;    J4=Z;
     =RM15;  E10M15; J22!=Z;
25;  E3M15;  J5!=Z;  E2M15;    =Q14;
6;   M0M14;
*3;  J7C14Z; DC14;   ZERO;     SHLD+6;   =C15;
     V1;     V0;     SHLDC15;
     J8<Z;   ERASE;  E6M15;    C15;      +;      E8M15; J9=Z;
10;  DUP;    =E4M15;
EXIT2;

4;   ERASE;  JSE4;
5;   ZERO;   =E3M15; E4M15;    DUP;
     SHC-6;  SHL-42; SET61; -;
     J11=Z;  E8M15;  J12=Z;    E2M15;    =Q14;   M0M14; REV;
12;
EXIT2;

9;   REV;    =M0M14; Q14;      =E2M15;   J10;
8;   ERASE;  C15;    SET6;     -;        DUP;    J13=Z;  NEG;    NOT;
     DUP;    J14!=Z; NOT;
13;  NOT;    SHL+6;  =E6M15;   J3;
14;  SET56;  -;      J3=Z;     ZERO;     =RC14;
     ZERO;   NOT;    =E9M15;
     ERASE;  SET61;  E6M15;    +;        =E4M15;
     ZERO;   =E8M15;
     Q14;    =E2M15; ZERO;     NOT;      NEG;
EXIT1;

7;   ERASE;  SET8;   =C14;
     ZERO;   NOT;    DUP;      =+I14;
     NEG;    =+M14;  I14;
     J6!=Z;  E5M15;  J15=Z;    E7M15;
17;  NOT;    DUP;    =E7M15;
     M15TOQ13;       =+M13;    E1M13;   =Q13;
     M13;    I13;    -;        NOT;     NEG;    =I14;
     I13;    =M14;
     SET8;   =C14;
     ZERO;   =TR;    PARQ13;   J18TR;   E9M15;  J19!=Z;
20;  E7M15;  NOT;
     M15TOQ13; =+M13;
     E1M13;  =Q13;
     PREQ13; J6;

15;  ZERO;   NOT;    =E5M15;
21;  E0M15;  =Q13;
     PREQ13;
     ZERO;   J17;

18;  ZERO;   NOT;     DUP;     =E10M15; =E9M15;
     ZERO;   =E6M15;
     ZERO;   =E8M15;
     ZERO;
EXIT1;

19;  ZERO;   =E9M15;  M0M14;   SHL-42;  SET61;  -;      J20!=Z;
     ZERO;   =E5M15;
     ZERO;   =RC14;
     Q14;    =E2M15;
     ZERO;   =E8M15;
     SET2;
EXIT1;

11;  ERASE;      NOT;      NEG;    ZERO;   =E8M15;
EXIT1;

22;  E0M15; =Q13;
23;  V2;     =M0M13;  PREQ13;
     ZERO;   =TR;     PARQ13;  J23TR;   SET8;   =C14;   M0M13;
26;  SHLD6;  SETB75; NEV;
     J27=Z;  DC14;    J26C14NZ; ERASE;
     J23;
27;  ERASE;  ZERO;    =RC14;
24;  ZERO;   =E10M15; J21;
2;   SHC-2;  SHL-46;  =Q15;  V0P54M15; (V0L54M15); DUP;    J4=Z;
     =RM15;  E10M15;  J25=Z;  J24;
(END OF L55);


(********************************************************************);
(       FETCH NUMBER,29.1O.64                                        );
(********************************************************************);
P56V10; (L56V10);
     V1=B0000037765600000;
     V3=1/15;
     V4=B3244647411363216;
     V5=B1067311107601054;
     V6=B2372617635665123;
     V7=B2567106670407501;
     V8=B6625327150150035;
     V10=B0000037760000000;
1;   SHC-2; SHL-46; DUP; =M15; =V0; (This is the
     floating point entry);
3;   ZERO; J40NV; NOT;
40;  =V9; V0P54M15; (V0L54M15); DUP; J4=Z; =M15; ZERO; NOT; =E8M15;
     V0;
*5;  JSP55;   (JS1L55);
EXIT1; (Error);
     (L55 uses an EXIT2 to get to here);
     =C13;    V1;      SHLC13;  J6>=Z;   VR;      C13;    V0;
     SHL+16;  SHL-16;  =V0;     Q0TOQ12;
     Q0TOQ13; =C13;
     ZERO;    C13;     SET30;   J7=;
     ZERO;    =V2;
     SET29;   -;       J11=Z;

9;   V10;     SHLC13;  J10>=Z;
     ZERO;    NOT;     =C12;
     DUP;     DUP;     +;       DUP;
     SHA+2;   +;       C13;     SET16;   -;       +;      J12V;
     REV;     ERASE;   I13;     J11=Z;
     V0;      V3;      -;       =V0;     ZERO;    NOT;    =I12;

11;  Q13;     CAB;     JS3P55; (JS3L55);
     (EXIT1); J13;
     (EXIT2); CAB;     =Q13;    =C13;
     REV;     J11C13Z; J9;

12;  ERASE;   I13;     J14=Z;   ZERO;    NOT;     =I12;
15;  DUP; MAX; ERASE;  J11;

10;  C13;     SET31;   -;       J16!=Z;  I13;     J17!=Z;
     ZERO;    NOT;     =I13;    J11;

16;  J19C12Z;
34;  V2;      J20>=Z;  NEG;
20;  ZERO;    C13;     SET27;   -;       J21!=Z;  ZERO;   NOT;   =M13;
22;  CAB;     Q13;     REV;     JS3P55; (JS3L55);
     (EXIT1); J23;
     (EXIT2); CAB;     =Q13;    =C13;
     PERM;    J22C13Z;

     C13;     SET30;   J24=;
     ZERO;    =V2;
     SET29;   -;       J25=Z;

26;  V10;     SHLC13;  J21>=Z;  ZERO;    NOT; =M12; DUP;
     +;       DUP;     SHA+2;   +;
     C13;     SET16;   -;       +;
25;  CAB;     Q13;     REV;     JS3P55;  (JS3L55);
     (EXIT1); J23;
     (EXIT2); CAB;     =Q13;    =C13;
     PERM;    J25C13Z; J26;

21;  V1;      SHLC13;  J18<Z;   V2;      J41>=Z; NEG;
41;  CAB;
36;  V0;      JSP57;  (JS1L57); Q12;     J27=Z;
     Q12;     Q13;    NEV;      SHL+16;  J28!=Z;
     VR;      V0;     =Q13;     C13;     +;
     V4;      xD;     SHAD+2;   DUPD;    ROUND;
     DUP;     =Q13;   ZERO;
     REV;     -D;     CONT;     DUP;     DUP;
     x;       SHA-7;  DUP;      V5;      x;
     V6;      +;      REV;      V7;      +;      V8;
     REV;     /;      +;        REV;
     SHA-4;   DUPD;   +;        PERM;
     -;       SHA+1;  /;        REV;     DUP;
     ZERO;    FLOAT;  FIX;      NEG;     =C12;
     ERASE;
     SHAC12;  x;      Q13;      SET48;   +;
     C12;     -;      V0;       SHL+16;  SHL-32; J33!=Z;
     REV;     DUP;    J42=Z;
     ZERO;    FLOAT;  STR;      PERM;    ZERO;
     SHAD+8;  CAB;    +;        SHAD-8;  DUP;
     J43<Z;
     CONT;    REV;    CONT; J33;

43;  ERASE;
42;  ERASE;   ERASE;  ZERO;
33;  V9;      SHA+48; ERASE;
EXIT2;

2;   SHC-2;  DUP;      ZERO;     NOT;   SHLD+2; =V0;
     ERASE;  SHL-46;   =M15;
     J3;  (This is the fixed point entry);
4;   ERASE;  JSE4;
6;   SETAR5; =LINK;    J3P55;  (J3L55);
7;   ZERO;   NOT;      =V2;
8;   ERASE;  J11;
14;  V0;     V3;       /;      =V0;   J15;
19;  NOT;    NEG;      J34;
24;  ZERO;   NOT;      =V2;    ERASE; J25;
27;  ERASE;  ERASE;
45;  V0;     SHC-2;    SHL-46; =M15;  J3;
28;  ERASE;  ERASE;    J39;
18;  ERASE;
17;  ERASE;
29;  JS3P55; (JS3L55); J35;    =C13;   J29C13Z;
     V10;;   SHLC13;   J30<Z;
     V1;     SHLC13;   J29<Z;
37;  V0;     JSP57;   (JS1L57);  Q12;    J45=Z;
39;  ZERO;   NOT;
EXIT1;

30;  ZERO;   NOT;      =C12;   J29;
13;  J31=Z; (Q13/INTEGER);
     REV;    V2;       J44>=Z; NEG;
44;  REV;    ZERO;
     (EXP ACC/Q13/SIGNED INTEGER);
     REV;
32;  =Q13;   V2;       J36>=Z; NEG; J36;
23;  J32!=Z; ERASE;
31;  ERASE;  ERASE;
38;  ZERO;
EXIT1;

35;  J37!=Z; J38;
(END OF L56);

(********************************************************************);
(       RETAIN CHARACTER,7.8.63                                      );
(********************************************************************);
P57; (L57);
     SHC-2;     SHL-46; =M15;   V0P54M15; (V0L54M15); DUP;   J1=Z;
     =M15;      ZERO;   NOT;    =E3M15;   E8M15; J2=Z;
     =M0M14;    Q14;    =E2M15; ZERO; = E8M15;
2;   EXIT1;
1;   ERASE;     JSE4;
(END OF L57);


(********************************************************************);
(       DEALLOCATE, 7.8.63                                           );
(********************************************************************);
P58; (L58);
     SHC-2;     SHL-46; =M15;  V0P54M15; (V0L54M15); ZERO;
     =V0P54M15; (=V0L54M15); =M15;   M0M15; SHL-32;
     SET6;      OUT;
EXIT1;
(END OF L58);
FINISH;



